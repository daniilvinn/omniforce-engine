#pragma lang : glsl
#pragma stage : task
#version 460 core

#extension GL_EXT_scalar_block_layout				: require
#extension GL_EXT_buffer_reference2					: require
#extension GL_EXT_nonuniform_qualifier				: require
#extension GL_EXT_mesh_shader						: require
#extension GL_KHR_shader_subgroup_arithmetic		: require
#extension GL_KHR_shader_subgroup_ballot			: require

#include "common.glslh" // common
#include "mesh_data.glslh" // mesh data
#include "render_object.glslh" // common render object data
#include "task_payload.glslh"
#include "frustum.glslh"
#include "virtual_geometry_utils.glslh"
#include "transform.glslh"

layout(local_size_x = __OMNI_TASK_SHADER_PREFERRED_WORK_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

shared uint s_actual_num_meshlets;

layout(push_constant, scalar) uniform pc_ {
    CameraData camera_data;
    MeshData mesh_data;
    RenderObjectData render_objects_data;
} pc;

void main()
{
	uint lid = gl_LocalInvocationIndex; // lid = lane id
	
	RenderObjectData render_object_data = pc.render_objects_data[gl_DrawID];

	Transform transform = render_object_data.transform;
	MeshData mesh_data = pc.mesh_data[render_object_data.geometry_data_id];

	if(gl_GlobalInvocationID.x >= mesh_data.meshlet_count) { // check if we are out of bounds
		return;
	}

	if(lid == 0) { // init shared memory in first lane
		s_actual_num_meshlets = 0;
	}

	MeshletCullBounds cull_bounds = mesh_data.meshlets_cull_bounds[gl_GlobalInvocationID.x];

	// Test this code with work groups of 64 threads and more
	bool is_optimal_lod = TestMeshletLOD(cull_bounds.lod_culling, pc.camera_data, render_object_data);
	bool is_visible = TestMeshletVisibility(cull_bounds, pc.camera_data, transform);

	// Compute offset using subgroup intrinsics
	uvec4 ballot = subgroupBallot(is_optimal_lod && is_visible);
	uint subgroup_offset_atomic = 0;

	if(subgroupElect()) {
		subgroup_offset_atomic = atomicAdd(s_actual_num_meshlets, subgroupBallotBitCount(ballot));
	}

	uint subgroup_offset = subgroupBroadcastFirst(subgroup_offset_atomic);

	// Cull
	if(is_optimal_lod && is_visible) {
		uint id_index = subgroup_offset + subgroupExclusiveAdd(1);
		task_payload.meshlet_ids[id_index] = uint8_t(gl_GlobalInvocationID.x & (gl_WorkGroupSize.x - 1));
	}

	// Synchronize access to `s_actual_num_meshlets`
	subgroupBarrier();

	// Emit mesh work groups
	if(lid == 0 && s_actual_num_meshlets != 0) {
		task_payload.base_offset = gl_GlobalInvocationID.x;
		EmitMeshTasksEXT(s_actual_num_meshlets, 1, 1);
	}
}

#pragma stage : mesh
#version 460 core

#extension GL_EXT_mesh_shader									: require
#extension GL_EXT_debug_printf									: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16		: require

#include "common.glslh"
#include "render_object.glslh"
#include "mesh_data.glslh"
#include "mesh_quantization.glslh"
#include "sphere.glslh"

layout(local_size_x = __OMNI_MESH_SHADER_PREFERRED_WORK_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 124) out;

layout(location = 0) out uint64_t out_material_bda[];

layout(push_constant, scalar) uniform pc_ {
    CameraData camera_data;
    MeshData mesh_data;
    RenderObjectData render_objects_data;
} pc;

layout(location = 1) out VertexOutput {
	vec3 position;
#ifdef __OMNI_HAS_VERTEX_TEXCOORDS
    vec2 uv[__OMNI_MESH_TEXCOORD_COUNT];
#endif
#ifdef __OMNI_HAS_VERTEX_NORMAL
	vec3 normal;
#endif
#ifdef __OMNI_HAS_NORMAL_MAP
	vec4 tangent;
#endif
} vertex_output[];

#include "task_payload.glslh"
#include "pcg_hash.glslh"

void main()
{
	RenderObjectData render_object_data = pc.render_objects_data[gl_DrawID];
	MeshData mesh_data = pc.mesh_data[render_object_data.geometry_data_id];

	Transform transform = render_object_data.transform;

	MeshMeshletsData meshlet_data = mesh_data.meshlets_data[task_payload.base_offset + task_payload.meshlet_ids[gl_WorkGroupID.x]];
	vec3 meshlet_center = mesh_data.meshlets_cull_bounds[task_payload.base_offset + task_payload.meshlet_ids[gl_WorkGroupID.x]].bounding_sphere.center;

	// Extract counts and bitrate from bit packed data
	uint meshlet_vertex_count =		ExtractMeshletVertexCount(meshlet_data.metadata);
	uint meshlet_triangle_count =	ExtractMeshletTriangleCount(meshlet_data.metadata);
	uint meshlet_bitrate =			ExtractMeshletBitrate(meshlet_data.metadata);

	// Optimization with early memory allocation to hide allocation latency
	if(gl_LocalInvocationIndex == 0) {
        SetMeshOutputsEXT(meshlet_vertex_count, meshlet_triangle_count);
    };

	// process vertex data
	uint vertex_processing_iterations = (meshlet_vertex_count + gl_WorkGroupSize.x - 1) / gl_WorkGroupSize.x;
	int bit_extend_bitmask = 1 << (meshlet_bitrate - 1);

	for(int i = 0; i < vertex_processing_iterations; i++) {
		if((gl_LocalInvocationIndex * vertex_processing_iterations + i) >= meshlet_vertex_count) {
			break;
		}

		// Compute offset
		uint vtx_offset = gl_LocalInvocationIndex * meshlet_bitrate * 3 * vertex_processing_iterations + (i * 3 * meshlet_bitrate);

		// Fetch vertex
		ivec3 encoded_vertex = FetchVertexFromStream(mesh_data.vertices, meshlet_bitrate, meshlet_data.vertex_bit_offset + vtx_offset);

		// Restore sign
		encoded_vertex = (encoded_vertex ^ bit_extend_bitmask) - bit_extend_bitmask;

		// Decode vertex
		vec3 vertex = DecodeVertex(
			encoded_vertex,
			mesh_data.quantization_grid_size, 
			meshlet_center
		);

		vertex = TransformPoint(vertex, transform);

		vertex_output[gl_LocalInvocationIndex * vertex_processing_iterations + i].position = vertex;
		gl_MeshVerticesEXT[gl_LocalInvocationIndex * vertex_processing_iterations + i].gl_Position = pc.camera_data.view_proj * vec4(vertex, 1.0f);

		// Process attributes
		out_material_bda[gl_LocalInvocationIndex * vertex_processing_iterations + i] = render_object_data.material_bda;
	
		#ifdef __OMNI_HAS_VERTEX_TEXCOORDS
			for(int j = 0; j < __OMNI_MESH_TEXCOORD_COUNT; j++) {
				vertex_output[gl_LocalInvocationIndex * vertex_processing_iterations + i].uv[j] = vec2(mesh_data.attributes[meshlet_data.vertex_offset + (gl_LocalInvocationIndex * vertex_processing_iterations + i)].uv[j]);
			}
		#endif

		// Process normals, tangents and bitangents
		#ifdef __OMNI_HAS_VERTEX_NORMAL
			f16vec3 normal = normalize(
				RotateByQuat(
					DecodeNormal(mesh_data.attributes[meshlet_data.vertex_offset + (gl_LocalInvocationIndex * vertex_processing_iterations + i)].normal), 
					transform.rotation
				)
			);
			vertex_output[gl_LocalInvocationIndex * vertex_processing_iterations + i].normal = vec3(normal);
		#endif
		#if defined(__OMNI_HAS_NORMAL_MAP) && defined(__OMNI_HAS_VERTEX_NORMAL) && defined(__OMNI_HAS_VERTEX_TANGENT)
			f16vec4 tangent = DecodeTangent(mesh_data.attributes[meshlet_data.vertex_offset + (gl_LocalInvocationIndex * vertex_processing_iterations + i)].tangent);
			tangent.xyz = normalize(tangent.xyz - dot(tangent.xyz, normal) * normal); // reorthogonalize

			vertex_output[gl_LocalInvocationIndex * vertex_processing_iterations + i].tangent = vec4(tangent);
		#endif

	}

	// process triangle data
	uint index_processing_iterations = (meshlet_triangle_count + gl_WorkGroupSize.x - 1) / gl_WorkGroupSize.x;
	for(int i = 0; i < index_processing_iterations; i++) {
		if(gl_LocalInvocationIndex * index_processing_iterations + i >= meshlet_triangle_count) {
			break;
		}

		gl_PrimitiveTriangleIndicesEXT[gl_LocalInvocationIndex * index_processing_iterations + i] = uvec3(
			mesh_data.micro_indices[meshlet_data.triangle_offset + ((gl_LocalInvocationIndex * index_processing_iterations + i) * 3)].micro_index,
			mesh_data.micro_indices[meshlet_data.triangle_offset + ((gl_LocalInvocationIndex * index_processing_iterations + i) * 3 + 1)].micro_index,
			mesh_data.micro_indices[meshlet_data.triangle_offset + ((gl_LocalInvocationIndex * index_processing_iterations + i) * 3 + 2)].micro_index
		);
	}
}

#pragma stage : fragment
#version 460 core

#extension GL_EXT_nonuniform_qualifier							: require
#extension GL_EXT_shader_explicit_arithmetic_types_float16		: require
#extension GL_EXT_debug_printf									: require

#include "common.glslh"
#include "mesh_data.glslh"
#include "render_object.glslh"
#include "texture.glslh"

layout(early_fragment_tests) in;

layout(push_constant, scalar) uniform pc_ {
    CameraData camera_data;
    MeshData mesh_data;
    RenderObjectData render_objects_data;
} pc;

layout(location = 0) flat in uint64_t v_MaterialBDA;

layout(location = 1) in Input {
	vec3 position;
#ifdef __OMNI_HAS_VERTEX_TEXCOORDS
    vec2 uv[__OMNI_MESH_TEXCOORD_COUNT];
#endif
#ifdef __OMNI_HAS_VERTEX_NORMAL
	vec3 normal;
#endif
#ifdef __OMNI_HAS_NORMAL_MAP
	vec4 tangent;
#endif
} v_InputAttributes;


layout(buffer_reference, scalar, buffer_reference_align = 16) readonly buffer Material {
	uint pipeline_id;
	#ifdef __OMNI_HAS_ALPHA_CUTOFF
        float alpha_cutoff;
    #endif
    #ifdef __OMNI_HAS_BASE_COLOR_FACTOR
        vec4 base_color_factor;
    #endif
    #ifdef __OMNI_HAS_BASE_COLOR_MAP
        Texture base_color_map;
    #endif
    #ifdef __OMNI_HAS_METALLIC_FACTOR
        float metallic_factor;
    #endif
    #ifdef __OMNI_HAS_METALLIC_ROUGHNESS_MAP
        Texture metallic_roughness_map;
    #endif
    #ifdef __OMNI_HAS_NORMAL_MAP
        Texture normal_map;
    #endif
    #ifdef __OMNI_HAS_OCCLUSION_MAP
        Texture occlusion_map;
    #endif
    #ifdef __OMNI_HAS_ROUGHNESS_FACTOR
        float roughness_factor;
    #endif
};

layout(location = 0) out vec4 out_position;
layout(location = 1) out vec4 out_color;
layout(location = 2) out vec4 out_normal;
layout(location = 3) out vec4 out_metallic_roughness_occlusion;

#ifdef __OMNI_HAS_NORMAL_MAP
	f16vec3 FetchNormal(Texture normal_map) {
	#ifndef __OMNI_HAS_VERTEX_TEXCOORDS
		return f16vec3(0.0f, 0.0f, 1.0f);
	#else
		f16vec3 normal = f16vec3(texture(texture_bank[normal_map.texture_index], vec2(v_InputAttributes.uv[normal_map.uv_channel_index].x, 1.0 - v_InputAttributes.uv[normal_map.uv_channel_index].y)).rgb);
		return normal * 2.0hf - 1.0hf;
	#endif
	}
#endif

void main() {
	Material material = Material(v_MaterialBDA);

	// Positions
	out_position = vec4(v_InputAttributes.position, 1.0f);

	// Base color
	vec4 color = vec4(1.0f, 0.0f, 1.0f, 1.0f);
	#ifdef __OMNI_HAS_BASE_COLOR_MAP
		color = texture(
			texture_bank[material.base_color_map.texture_index], 
			vec2(v_InputAttributes.uv[material.base_color_map.uv_channel_index].x, 
			1.0f - v_InputAttributes.uv[material.base_color_map.uv_channel_index].y)
		);
		#ifdef __OMNI_HAS_ALPHA_CUTOFF // alpha masking
			//if(color.a <= material.alpha_cutoff) discard;
		#endif
	#endif
    out_color = color;

	// Normal
	#if defined(__OMNI_HAS_NORMAL_MAP) && defined(__OMNI_HAS_VERTEX_NORMAL) && defined(__OMNI_HAS_VERTEX_TANGENT)
		f16vec4 T = f16vec4(v_InputAttributes.tangent);
		f16vec3 N = f16vec3(v_InputAttributes.normal);
		f16vec3 B = cross(N, T.xyz) * T.w;

		N = normalize(f16mat3(T.xyz,B,N) * FetchNormal(material.normal_map));
		out_normal = vec4(vec3(N), 1.0f);
	#else 
		out_normal = vec4(v_InputAttributes.normal, 1.0f);
	#endif

	// Metallic roughness occlusion
	vec3 metallic_roughness_occlusion = vec3(0.0f, 0.0f, 1.0f);
	#ifdef __OMNI_HAS_METALLIC_ROUGHNESS_MAP
		metallic_roughness_occlusion = texture(
		texture_bank[material.metallic_roughness_map.texture_index], 
		vec2(v_InputAttributes.uv[material.metallic_roughness_map.uv_channel_index].x, 
		1.0f - v_InputAttributes.uv[material.metallic_roughness_map.uv_channel_index].y)
	).xyz;
	#endif
	out_metallic_roughness_occlusion = vec4(metallic_roughness_occlusion, 1.0f);

}