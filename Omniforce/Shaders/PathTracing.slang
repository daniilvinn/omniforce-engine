#pragma once

import Common;
import Gen.PathTracingInput;
import Lighting;
import Compression;
import Rendering;
import Random;
import Sampler;

struct RayPayload
{
    float3 LightColor;
    float3 PathThroughput;
    float3 IncomingDirection;
    float3 HitPoint;
    uint RandomSeed;
};

using namespace Omni;

[shader("raygeneration")]
void PathConstruction()
{
    uint3 PixelIndex = DispatchRaysIndex();
    uint3 ImageSize = DispatchRaysDimensions();
    uint RayID = PixelIndex.z * ImageSize.x * ImageSize.y +
                 PixelIndex.y * ImageSize.x +
                 PixelIndex.x;

    ViewData* Camera = Input.View;

    // Setup primary rays
    float2 Ndc = (float2(PixelIndex.xy) + 0.5) / float2(Camera.viewport_width, Camera.viewport_height) * 2.0 - 1.0;
    Ndc.y = -Ndc.y;

    float AspectRatio = Camera.viewport_width / Camera.viewport_height;
    float ScaleY = tan(Camera.fov * 0.5);
    float ScaleX = ScaleY * AspectRatio;

    float3 RayDirViewSpace = normalize(float3(Ndc.x * ScaleX, Ndc.y * ScaleY, -1.0));
    float3 Direction = normalize(mul(transpose(Camera.view), float4(RayDirViewSpace, 0.0)).xyz);
    float3 Origin = Camera.position;

    RayPayload Payload;
    Payload.LightColor = 0.0f;
    Payload.PathThroughput = 1.0f;
    Payload.IncomingDirection = Direction;
    Payload.HitPoint = Origin;

    RandomNumber<PCGGenerator> Random = RandomNumber<PCGGenerator>(Input.RandomSeed + RayID);
    Payload.RandomSeed = Random.Get();

    const uint NumBounces = 2;

    // +1 because primary ray is not generated by bounce
    // Trace until bounce limit is reached or no surface was hit
    for (uint i = 0; i < NumBounces + 1; i++) {
        RayDesc Ray;
        Ray.Origin = Payload.HitPoint;
        Ray.Direction = Payload.IncomingDirection;
        Ray.TMin = 0.001;
        Ray.TMax = Camera.far_clip_distance;

        Payload.HitPoint = FloatMax;

        TraceRay(
            SceneTLAS,
            RAY_FLAG_NONE,
            0xFF,
            0,   
            1,   
            0,   
            Ray,
            Payload
        );

        if (IsInvalidHitPoint(Payload.HitPoint)) {
            break;
        }
    }

    // Compute final color, get history, blend and write back
    float3 FinalColor = Payload.LightColor * Payload.PathThroughput;

    if (Input.AccumulatedFrames == 1) {
        OutputImage[PixelIndex.xy] = float4(FinalColor, 1.0f);
    } else {
        float3 History = OutputImage[PixelIndex.xy].rgb;
        OutputImage[PixelIndex.xy] = float4(lerp(History, FinalColor, 1.0f / Input.AccumulatedFrames), 1.0f);
    }
}

[shader("closesthit")]
void PathVertex(inout RayPayload Payload, in BuiltInTriangleIntersectionAttributes Attr)
{
    InstanceRenderData Instance = Input.Instances[InstanceID()];
    GeometryMeshData Geometry = Input.Meshes[Instance.geometry_data_id];
    GeometryLayoutTable Layout = Geometry.ray_tracing.layout;
    uint3 HitTriangleIndices = Geometry.ray_tracing.indices[PrimitiveIndex()];

    float3 Barys = float3(1.0 - Attr.barycentrics.x - Attr.barycentrics.y, Attr.barycentrics);

    RTMaterial* Material = (RTMaterial*)Instance.material_address;

    // Fetch triangle positions
    float3 WorldPosition = 0;
    for (uint i = 0; i < 3; i++) {
        WorldPosition += HitTriangleVertexPosition(i) * Barys[i];
    }
    WorldPosition = Instance.transform.TransformPoint(WorldPosition);

    uint LastFetchedUVIndex = (uint)-1;

    // Fetch base color
    float2 UV = 0;
    float3 BaseColor = Material.UniformColor.xyz;
    if (Material.Metadata.HasBaseColor == 1) {
        UV = Geometry.RTFetchUV(HitTriangleIndices, Material.BaseColor.UVChannelIndex, Barys);
        BaseColor = TextureHeap[Material.BaseColor.TextureIndex].SampleLevel(UV, 0.0).rgb;
        LastFetchedUVIndex = Material.BaseColor.UVChannelIndex;
    }

    // Fetch vertex colors
    if (Layout.AttributeMask.HasColor == 1) {
        VertexDecoder Decoder;

        float3 VertexColor = 0;
        for (uint i = 0; i < 3; i++) {
            uint AttrOffset = Layout.Stride * HitTriangleIndices[i];
            uint8_t4 FetchedColor = *(uint8_t4 *)(Geometry.ray_tracing.attributes + AttrOffset + Layout.Offsets.Color);
            VertexColor += Decoder.DecodeColor(FetchedColor).rgb * Barys[i];
        }
        BaseColor *= VertexColor;
    }

    // Fetch vertex normals
    float3 GeometryNormal = 0;
    if (Material.Metadata.HasNormal == 1) {
        VertexDecoder Decoder;

        for (uint i = 0; i < 3; i++) {
            uint AttrOffset = Layout.Stride * HitTriangleIndices[i];
            half2 FetchedNormal = *(half2 *)(Geometry.ray_tracing.attributes + AttrOffset + Layout.Offsets.Normal);
            GeometryNormal += Decoder.OctahedronDecode(FetchedNormal) * Barys[i];
        }
    } else {
        GeometryNormal = float3(0, 0, 1);
    }

    // Fetch normal map
    float3 Normal = normalize(GeometryNormal);
    float4 Tangent = 0.0;
    float3 Bitangent = 0.0;
    if (Material.Metadata.HasNormal == 1 && Layout.AttributeMask.HasTangents == 1) {
        // Check if corresponding UVs are loaded; reload otherwise
        if (LastFetchedUVIndex != Material.Normal.UVChannelIndex) {
            UV = Geometry.RTFetchUV(HitTriangleIndices, Material.Normal.UVChannelIndex, Barys);
            LastFetchedUVIndex = Material.BaseColor.UVChannelIndex;
        }

        // Fetch tangents
        VertexDecoder Decoder;
        for (uint i = 0; i < 3; i++) {
            uint AttrOffset = Layout.Stride * HitTriangleIndices[i];
            half2 FetchedTangent = *(half2 *)(Geometry.ray_tracing.attributes + AttrOffset + Layout.Offsets.Tangent);
            Tangent += Decoder.DecodeTangent(FetchedTangent) * Barys[i];
        }
        Tangent = normalize(Tangent);
        
        // Compute bitangent
        Bitangent = cross(GeometryNormal, Tangent.xyz) * Tangent.w;

        // Fetch normal map, decode and transform
        float3 NormalMapTexel = TextureHeap[Material.Normal.TextureIndex].SampleLevel(UV, 0.0).rgb;
        NormalMapTexel = Decoder.DecodeNormalMap(NormalMapTexel);

        float3x3 TBN = float3x3(
            Tangent.xyz,
            Bitangent,
            GeometryNormal
        );

        Normal = mul(NormalMapTexel, TBN);

        // Build TBN matrix for a normal from normal map
        Tangent.xyz = normalize(Tangent.xyz - dot(Tangent.xyz, Normal) * Normal);
        Bitangent = cross(Normal, Tangent.xyz);
    } else {
        Tangent.xyz = normalize(cross(Normal, float3(0, 1, 0)));
        Bitangent = cross(Normal, Tangent.xyz);
    }

    float3 N = QuaternionRotate(float4(Instance.transform.rotation), Normal);
    Tangent.xyz = QuaternionRotate(float4(Instance.transform.rotation), Tangent.xyz);
    Bitangent = QuaternionRotate(float4(Instance.transform.rotation), Bitangent);
    Payload.HitPoint = WorldPosition + N * 0.0001f;

    // Fetch metallic roughness
    float3 MetallicRoughnessOcclusion = float3(1.0f, 1.0f, 0.0f);
    if (Material.Metadata.HasMetallicRoughness == 1) {
        if (LastFetchedUVIndex != Material.MetallicRoughness.UVChannelIndex) {
            UV = Geometry.RTFetchUV(HitTriangleIndices, Material.MetallicRoughness.UVChannelIndex, Barys);
            LastFetchedUVIndex = Material.BaseColor.UVChannelIndex;
        }

        MetallicRoughnessOcclusion = TextureHeap[Material.MetallicRoughness.TextureIndex].SampleLevel(UV, 0.0).rgb;
    }

    // Compute BRDF
    float Metallic = MetallicRoughnessOcclusion.b;
    float Roughness = MetallicRoughnessOcclusion.g;

    // Ensure consistent view direction (toward surface)
    float3 V = normalize(-Payload.IncomingDirection);

    float3 F0 = 0.04f;
    F0 = lerp(F0, BaseColor, Metallic);

    float3 L;

    RandomNumber<PCGGenerator> Random = RandomNumber<PCGGenerator>(Payload.RandomSeed);
    float u1 = float(Random.Get() & 0xFFFF) / 65535.0f;
    float u2 = float(Random.Get() & 0xFFFF) / 65535.0f;
    float2 Xi = float2(u1, u2);

    float3 BRDFWeight;
    float PDF;

    CookTorranceBRDFSampler Sampler;
    Sampler.Sample(
        N,
        Tangent.xyz,
        Bitangent,
        V,
        BaseColor,
        Metallic,
        Roughness,
        F0,
        Xi,
        L,
        BRDFWeight,
        PDF
    );

    // Validate and apply BRDF weight
    if (PDF > 1e-8 && isfinite(BRDFWeight.x) && isfinite(BRDFWeight.y) && isfinite(BRDFWeight.z)) {
        Payload.PathThroughput *= BRDFWeight;
        Payload.IncomingDirection = L;
    } else {
        // Terminate path on invalid sample
        Payload.PathThroughput = float3(0.0, 0.0, 0.0);
    }

    Payload.RandomSeed = Random.Get();
    Payload.HitPoint = WorldPosition + N * 0.0001f;
}

[shader("closesthit")]
void PointLightHit(inout RayPayload Payload, in BuiltInTriangleIntersectionAttributes Attr) {
    PointLight Light = Input.PointLights.Lights[InstanceID()];
    Payload.LightColor = Light.Color * Light.Intensity;
}

[shader("miss")]
void Miss(inout RayPayload Payload)
{
    //return;
    if (WorldRayDirection().y >= 0.0f) {
        Payload.LightColor = float3(0.7, 0.9, 1.0) * 2;
    } else {
        Payload.LightColor = float3(0.0, 0.0, 0.0);
    } 
}